##
# "Image" element
#
prototype(Neos.Demo:Content.Fragment.Image.Figure) < prototype(Neos.Fusion:Component) {
    src = Neos.Fusion:Case {
        asset {
            condition = ${q(node).property('image')}
            renderer = Neos.Neos:ImageUri {
                asset = ${q(node).property('image')}
            }
        }
        dummy {
            condition = ${node.context.inBackend}
            renderer = Neos.Fusion:ResourceUri {
                path = 'resource://Neos.Neos/Public/Images/dummy-image.svg'
            }
        }
    }

    alt = ''
    title = ''

    caption = Neos.Neos:Editable {
        property = 'caption'
    }

    renderer = afx`
        <figure>
            <img src={props.src} title={props.title} alt={props.alt} />
            <figcaption @if.has={props.caption}>
                {props.caption}
            </figcaption>
        </figure>
    `
}

